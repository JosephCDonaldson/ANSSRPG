buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'org.ajoberstar:gradle-git:1.2.0'
    }
}

import org.ajoberstar.grgit.*

ext.repo = Grgit.open(project.file('.'))

def gitHash = 'unknown'
if (new File(projectDir, '.git').exists()) {
    def repo = Grgit.open(project.file('.'))
    gitHash = repo.log().find().abbreviatedId
}

apply plugin: 'forge'

group = package_group
archivesBaseName = mod_name
def mod_version = "v${mod_version_major}.${mod_version_minor}."+gitHash
version = "${mc_version}-${mod_version}"


minecraft {
    version = "${mc_version}-${forge_version}-${mc_version}"
    runDir = "run"

    replace "@VERSION@", project.version
}




processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

targetCompatibility=7
sourceCompatibility=7

jar {
    classifier = ''
    manifest.mainAttributes(
            "Built-By": System.getProperty('user.name'),
            "Created-By": "${System.getProperty('java.vm.version')} + (${System.getProperty('java.vm.vendor')})",
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Git-Hash": gitHash
    )
}

// Source Jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// Javadoc Jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

// Deobfuscated Jar
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

tasks.build.dependsOn sourceJar, javadocJar, deobfJar

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}

//IDEA assets fix
sourceSets {
    main { output.resourcesDir = output.classesDir }
}

task tagRelease {
  doLast {
    println 'Attempting to apply tag ' + version + ' to commit ' + gitHash
    repo.tag.add {
        name = version
        message = "Release of ${version}"
        force = true
        pointsTo = gitHash
    }
  }
}